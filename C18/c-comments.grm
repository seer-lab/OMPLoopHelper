% Overrides to preserve comments in C
% Jim Cordy, March 2001

% Revised for C18 - JC 13 Nov 2020

% These overrides are an approximation - only the most common placements of C++ comments are handled.  
% Properly handling arbitrarily placed comments requires a robust grammar of somewhat greater sophistication.

#pragma -comments

% Can have comments as statements 
redefine statement
	...
    |	[comment]
end redefine

% Can have comments as declarations
%redefine declaration
%	...
%    |	[comment]
%end redefine

redefine function_definition
	...
    |	[comment]
end redefine

% Can have comments before initial values ...
redefine initializer
	[repeat comment] ...
end redefine

% ... or before =initializers 
redefine initialization
	[repeat comment] ...
end redefine

% Can have comments before formal parameters ...
redefine parameter_declaration
	[repeat comment] ...
end redefine

% ... or after formal parameters ...
redefine parameter_declaration
	...  [repeat comment]
end redefine

% ... or after a parameter list 
redefine parameter_type_list
	... [repeat comment]
end redefine

% Can have comments after an if condition or switch selector
redefine if_statement
    'if ( [expression] ) [repeat comment]
        [statement] [opt else_statement]
end redefine

redefine switch_statement
    'switch ( [expression] ) [repeat comment] 
        [statement]
end redefine 

% Can have comments on a block
redefine compound_statement
	[repeat comment] ...
end redefine

% Can have comments on a preprocessor statement
#ifdef PREPROCESSOR
redefine preprocessor
		[EX] '#endif [opt comment] [NL]
	|	...
end redefine

redefine preprocessor
	... [opt comment]
end redefine
#endif

% Can have comments before an expression
redefine expression
	[repeat comment] ...
end redefine

%%%% Observed other cases %%%%

% Strangely, can have a comment inside a for statement control clause
redefine semi_opt_expression_list
	[opt comment] ...
end redefine 

% Can have comments inside structs
redefine struct_declaration
	...  
    |	[comment] 
end redefine

redefine struct_or_union_body
    [repeat comment] ...
end redefine

% Can have comments inside assignments and logical expressions
redefine assignment_operator
    [repeat comment] ...
end redefine

redefine assignment_operator
    ... [repeat comment] 
end redefine

redefine logical_OR_operator
    [repeat comment] ...
end redefine

redefine logical_OR_operator
    ... [repeat comment] 
end redefine

redefine logical_AND_operator
    [repeat comment] ...
end redefine

redefine logical_AND_operator
    ... [repeat comment] 
end redefine

